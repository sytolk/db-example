
description = 'Fermat DataBase Server Node Example'
dependencies {
    compile group: 'org.jooq', name: 'jooq', version:'3.7.3'
    compile group: 'org.xerial', name: 'sqlite-jdbc', version:'3.8.11.2'
    compile group: 'org.springframework', name: 'spring-context', version:'4.2.5.RELEASE'
    compile group: 'org.springframework', name: 'spring-tx', version:'4.2.5.RELEASE'
    compile group: 'org.springframework', name: 'spring-jdbc', version:'4.2.5.RELEASE'
    compile group: 'com.zaxxer', name: 'HikariCP', version:'2.4.5'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.springframework', name: 'spring-test', version:'4.2.5.RELEASE'
}

test {
    environment "envTarget", "dev"
}

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        classpath 'org.jooq:jooq-codegen:3.7.3'
        classpath 'org.xerial:sqlite-jdbc:3.8.11.2'
    }
}

// Use your favourite XML builder to construct the code generation configuration file
// ----------------------------------------------------------------------------------
def writer = new StringWriter()
def xml = new groovy.xml.MarkupBuilder(writer)
        .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.7.0.xsd') {
    jdbc() {
        driver('org.sqlite.JDBC')
        url('jdbc:sqlite:node.db')
        /*user('sa')
        password('')*/
    }
    generator() {
        database() {
        }

        // Watch out for this caveat when using MarkupBuilder with "reserved names"
        // - https://github.com/jOOQ/jOOQ/issues/4797
        // - http://stackoverflow.com/a/11389034/521799
        // - https://groups.google.com/forum/#!topic/jooq-user/wi4S9rRxk4A
        generate([:]) {
            pojos true
            daos true
        }
        target() {
            packageName('com.bitdubai.fermat.db.sqlite.generated')
            directory('src/main/java')
        }
    }
}

// Run the code generator
// ----------------------
org.jooq.util.GenerationTool.generate(
        javax.xml.bind.JAXB.unmarshal(new StringReader(writer.toString()), org.jooq.util.jaxb.Configuration.class)
)